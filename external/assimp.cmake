if(NOT DEP_CMAKE_PATH OR NOT DEP_INSTALL_ROOT)
  message(FATAL_ERROR "DEP_CMAKE_PATH and DEP_INSTALL_ROOT must be set before including modules defined within /external")
endif()

########################################################################
# Install Assimp
########################################################################
set(libAssimp assimp)
ExternalProject_Add(${libAssimp}
PREFIX			${DEP_CMAKE_PATH}/${libAssimp}
GIT_REPOSITORY  https://github.com/assimp/assimp.git
GIT_TAG         v5.0.1
GIT_SHALLOW     ON
UPDATE_COMMAND 	""
CMAKE_ARGS      -DCMAKE_INSTALL_PREFIX:PATH=${DEP_INSTALL_ROOT}/${libAssimp}
				-DASSIMP_BUILD_ALL_IMPORTERS_BY_DEFAULT:BOOL=OFF
				-DASSIMP_BUILD_ALL_EXPORTERS_BY_DEFAULT:BOOL=OFF
				-DASSIMP_BUILD_ASSIMP_TOOLS:BOOL=OFF
				-DASSIMP_BUILD_TESTS:BOOL=OFF
				-DASSIMP_BUILD_FBX_IMPORTER:BOOL=ON
				-DASSIMP_BUILD_OBJ_IMPORTER:BOOL=ON
				-DASSIMP_BUILD_OBJ_EXPORTER:BOOL=ON
				-DASSIMP_BUILD_ZLIB:BOOL=ON
				-DASSIMP_LIBRARY_SUFFIX:STRING=
				-DLIBRARY_SUFFIX:STRING=
				-DCMAKE_DEBUG_POSTFIX:STRING=
				-DASSIMP_INJECT_DEBUG_POSTFIX:BOOL=OFF
				-DBUILD_SHARED_LIBS:BOOL=OFF
				-DCMAKE_POLICY_DEFAULT_CMP0091:STRING=NEW
				-DCMAKE_MSVC_RUNTIME_LIBRARY:STRING=MultiThreaded$<$<CONFIG:Debug>:Debug>
)


add_library(ASSIMP_LIBRARY STATIC IMPORTED)
set_target_properties(ASSIMP_LIBRARY PROPERTIES IMPORTED_LOCATION ${DEP_INSTALL_ROOT}/${libAssimp}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}assimp${CMAKE_STATIC_LIBRARY_SUFFIX})

add_library(IRRXML_LIBRARY STATIC IMPORTED)
set_target_properties(IRRXML_LIBRARY PROPERTIES IMPORTED_LOCATION ${DEP_INSTALL_ROOT}/${libAssimp}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}IrrXML${CMAKE_STATIC_LIBRARY_SUFFIX})

add_library(ZLIB_LIBRARY STATIC IMPORTED)
set_target_properties(
ZLIB_LIBRARY 
PROPERTIES 
	IMPORTED_LOCATION_RELEASE ${DEP_INSTALL_ROOT}/${libAssimp}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}zlibstatic${CMAKE_STATIC_LIBRARY_SUFFIX}
	IMPORTED_LOCATION_MINSIZEREL ${DEP_INSTALL_ROOT}/${libAssimp}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}zlibstatic${CMAKE_STATIC_LIBRARY_SUFFIX}
	IMPORTED_LOCATION_RELWITHDEBINFO ${DEP_INSTALL_ROOT}/${libAssimp}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}zlibstatic${CMAKE_STATIC_LIBRARY_SUFFIX}
	IMPORTED_LOCATION_DEBUG ${DEP_INSTALL_ROOT}/${libAssimp}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}zlibstaticd${CMAKE_STATIC_LIBRARY_SUFFIX}
)

set(ASSIMP_INCLUDE_DIR ${DEP_INSTALL_ROOT}/${libAssimp}/include)